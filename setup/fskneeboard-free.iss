; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#ifndef ApplicationVersion
#define ApplicationVersion "0.0.1"
#endif

#define MyAppName "FSKneeboard FREE"
#define MyAppPublisher "Christian-Alexander Hoffmann"
#define MyAppURL "https://www.fskneeboard.com/"
#define MyAppExeName "fskneeboard.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{BFD7EA7D-F475-4DEE-AE14-52129352949E}
AppName={#MyAppName}
AppVersion={#ApplicationVersion}
AppVerName={#MyAppName} v{#ApplicationVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=..\LICENSE.txt
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=..\dist
OutputBaseFilename=Install-FSKneeboard-FREE-v{#ApplicationVersion}
SetupIconFile=..\fskneeboard-server\vfrmap\winres\fskneeboard.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\dist\free\fskneeboard-server\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\free\CHANGELOG.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\free\README.pdf"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "..\dist\free\fskneeboard-server\repair-fskneeboard.bat"; DestDir: "{app}"; Flags: ignoreversion

Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\layout.json"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\manifest.json"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\InGamePanels\christian1984-ingamepanel-fskneeboard.spb"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\InGamePanels"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\icons\toolbar\ICON_TOOLBAR_CHRISTIAN1984_INGAMEPANEL_VFRMAPFORVR.svg"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\icons\toolbar"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\Textures\Menu\toolbar\ICON_TOOLBAR_CHRISTIAN1984_INGAMEPANEL_VFRMAPFORVR.svg"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\Textures\Menu\toolbar"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.css"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.html"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.js"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "{code:GetSimConnectPath}"; DestDir: "{app}"; DestName: "SimConnect.dll"; Flags: external uninsneveruninstall; Check: GetShouldInstallSimConnect
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\FSKneeboard\Start {#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\Repair {#MyAppName}"; Filename: "{app}\repair-fskneeboard.bat"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\Docs - Readme"; Filename: "{app}\README.pdf"
Name: "{autoprograms}\FSKneeboard\Docs - Changelog"; Filename: "{app}\CHANGELOG.md"
Name: "{autoprograms}\FSKneeboard\Web - Join us on Discord"; Filename: "https://discord.fskneeboard.com"
Name: "{autoprograms}\FSKneeboard\Web - Upgrade to PRO"; Filename: "https://fskneeboard.com/buy-now"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "https://discord.fskneeboard.com"; Flags: nowait shellexec runasoriginaluser postinstall; Description: "Join us on Discord!"
Filename: "https://fskneeboard.com/buy-now"; Flags: nowait shellexec runasoriginaluser postinstall; Description: "Upgrade to PRO!"

[UninstallDelete]
;This works if it is installed in custom location
Type: files; Name: "{app}\latestcheck.json"; 
Type: filesandordirs; Name: "{app}\logs"; 

[Code]
var
  CommunityFolderDirWizardPage: TInputDirWizardPage;
  CommunityFolderDir: String;
  CommunityFoldersListPage: TInputOptionWizardPage;
  DetectedCommunityFolders: TArrayOfString;
  CommunityFolderVersions: TArrayOfString;

  SimConnectWizardPage: TOutputMsgWizardPage;
  SimConnectFileEdit: TEdit;
  SimConnectFileButton: TButton;
  SimConnectPath: String;
  ShouldInstallSimConnect: Boolean;

// Forward declarations for common functions/procedures
function ParseUserCfgOpt(FilePath: String): String; forward;
procedure DiscoverCommunityFolders(); forward;
function GetCommunityFolderDir(Value: string): string; forward;
function StrSplit(Text: String; Separator: String): TArrayOfString; forward;
function DirCopy(SourcePath, DestPath: String; Overwrite: Boolean): Boolean; forward;
procedure CreateSimConnectWizardPage(AfterID: Integer); forward;

function GetShouldInstallSimConnect: Boolean;
begin
  Result := ShouldInstallSimConnect;
end;

procedure InitializeWizard;
var
  AfterID: Integer;
  communityFolderSuccess: Boolean;
  communityFolder: String;
  winstoreCommunityFolder: String;
  steamCommunityFolder: String;
  communityFolderDirWizardDescription: String;
  i: Integer;
  FolderCount: Integer;

begin
  AfterID := wpSelectDir;
  communityFolderSuccess := False;

  // First try the new UserCfg.opt-based discovery
  DiscoverCommunityFolders();
  FolderCount := GetArrayLength(DetectedCommunityFolders);

  if FolderCount > 1 then
  begin
    // Multiple community folders found - create selection page
    CommunityFoldersListPage := CreateInputOptionPage(
      AfterID,
      'Select Community Folder',
      'Multiple Microsoft Flight Simulator installations detected!',
      'Please select the Community Folder where you want to install the FSKneeboard panel:',
      True, False);

    for i := 0 to FolderCount - 1 do
    begin
      CommunityFoldersListPage.Add(CommunityFolderVersions[i] + ': ' + DetectedCommunityFolders[i]);
    end;
    CommunityFoldersListPage.Values[0] := True; // Select first one by default

    AfterID := CommunityFoldersListPage.ID;
    communityFolderSuccess := True;
  end
  else if FolderCount = 1 then
  begin
    // Single community folder found - use it directly
    communityFolder := DetectedCommunityFolders[0];
    communityFolderSuccess := True;
    communityFolderDirWizardDescription := 'SUCCESS! Automatically detected your ' + CommunityFolderVersions[0] + ' Community Folder.';
    Log('Auto-detected community folder via UserCfg.opt: ' + communityFolder);
  end
  else
  begin
    // No folders found via UserCfg.opt, fall back to hardcoded detection
    winstoreCommunityFolder := ExpandConstant('{localappdata}\Packages\Microsoft.FlightSimulator_8wekyb3d8bbwe\LocalCache\Packages\Community');
    steamCommunityFolder := ExpandConstant('{localappdata}\Packages\Microsoft.FlightDashboard_8wekyb3d8bbwe\LocalCache\Packages\Community');
    
    if DirExists(winstoreCommunityFolder) or DirExists(steamCommunityFolder) then begin
      communityFolderSuccess := True;
      communityFolderDirWizardDescription := 'SUCCESS! Automatically detected your Flight Simulator Community Folder (fallback method).';

      if DirExists(steamCommunityFolder) then begin
          communityFolder := steamCommunityFolder;
          Log('steamCommunityFolder found via fallback!');
      end else if DirExists(winstoreCommunityFolder) then begin
          communityFolder := winstoreCommunityFolder;
          Log('winstoreCommunityFolder found via fallback!');
      end
    end else begin
      communityFolderDirWizardDescription := '*** PLEASE DO NOT CLICK NEXT BEFORE READING THIS ***'#13#10#13#10
      + 'WARNING: Your Flight Simulator Community Folder could NOT be auto-detected! Please set AND VERIFY the path to your community folder manually:'#13#10#13#10
      + '- WINDOWS STORE USERS (MSFS 2020): C:\Users\[username]\AppData\Local\Packages\Microsoft.FlightSimulator_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10
      + '- WINDOWS STORE USERS (MSFS 2024): C:\Users\[username]\AppData\Local\Packages\Microsoft.Limitless_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10#13#10
      + '- STEAM USERS (MSFS 2020): C:\Users\[username]\AppData\Local\Packages\Microsoft.FlightDashboard_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10
      + '- STEAM USERS (MSFS 2024): Varies by installation - check your UserCfg.opt file';

      communityFolder := 'C:\'
    end;
  end;

  // Only create the manual input page if we don't have the multi-selection page
  if FolderCount <= 1 then
  begin
    CommunityFolderDirWizardPage := CreateInputDirPage(
          AfterID,
          'Select Community Folder Location',
          'Please tell us where your Flight Simulator Community Folder is located!',
          communityFolderDirWizardDescription,
          False, '');
    CommunityFolderDirWizardPage.Add('IMPORTANT: If the FSKneeboard-Ingame-Panel does NOT appear inside the game, then double-check that you have this directory right!' + #13#10#13#10 + 'Microsoft Flight Simulator Community Folder:');
    CommunityFolderDirWizardPage.Values[0] := communityFolder;

    if communityFolderSuccess then begin
      CommunityFolderDirWizardPage.SubCaptionLabel.Font.Color := clGreen;
    end else begin
      CommunityFolderDirWizardPage.SubCaptionLabel.Font.Color := clRed;
    end;

    CommunityFolderDirWizardPage.SubCaptionLabel.Font.Style := [fsBold];

    CommunityFolderDirWizardPage.PromptLabels[0].Font.Color := $0088FF;
    CommunityFolderDirWizardPage.PromptLabels[0].Font.Style := [fsBold];

    AfterID := CommunityFolderDirWizardPage.ID;
  end;
end;

function NextButtonClick(CurrPageID: Integer): Boolean;
var
  i: Integer;
begin
  if CurrPageID = wpSelectDir then
  begin
    // Check if SimConnect.dll exists after the directory is selected
    ShouldInstallSimConnect := not FileExists(ExpandConstant('{app}\SimConnect.dll'));
    if ShouldInstallSimConnect then
    begin
      CreateSimConnectWizardPage(CurrPageID);
    end;
  end
  else if ShouldInstallSimConnect and (CurrPageID = SimConnectWizardPage.ID) then
  begin
    SimConnectPath := SimConnectFileEdit.Text;
    if not FileExists(SimConnectPath) then
    begin
      MsgBox('The file ' + SimConnectPath + ' does not exist. Please select a valid SimConnect.dll file.', mbError, MB_OK);
      Result := False;
      Exit;
    end;
  end
  else if (GetArrayLength(DetectedCommunityFolders) > 1) and (CurrPageID = CommunityFoldersListPage.ID) then
  begin
    // Handle single folder selection from radio buttons
    for i := 0 to GetArrayLength(DetectedCommunityFolders) - 1 do
    begin
      if CommunityFoldersListPage.Values[i] then
      begin
        CommunityFolderDir := DetectedCommunityFolders[i];
        break;
      end;
    end;
    Log('Selected Community Folder: ' + CommunityFolderDir);
  end
  else if (GetArrayLength(DetectedCommunityFolders) <= 1) and (CurrPageID = CommunityFolderDirWizardPage.ID) then
  begin
    CommunityFolderDir := CommunityFolderDirWizardPage.Values[0];
    Log('CommunityFolderDir is: ' + CommunityFolderDir);
  end;
  Result := True;
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result := False;

  if ShouldInstallSimConnect and (PageID = SimConnectWizardPage.ID) then
    Result := False
  else if (not ShouldInstallSimConnect) and (SimConnectWizardPage <> nil) and (PageID = SimConnectWizardPage.ID) then
    Result := True;
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo,
  MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
var
  S: String;
  FolderPaths: TArrayOfString;
  i: Integer;
begin
  S := 'FSKneeboard FREE is Ready for Installation!' + NewLine;
  S := S + NewLine;
  S := S + 'FSKneeboard Server Component will be installed to:' + NewLine;
  S := S + Space + ExpandConstant('{app}') + NewLine;
  S := S + NewLine;

  S := S + 'FSKneeboard Ingame Panel will be installed to:' + NewLine;
  S := S + Space + CommunityFolderDir + '\christian1984-ingamepanel-fskneeboard' + NewLine;

  S := S + NewLine;

  if ShouldInstallSimConnect then
  begin
    S := S + 'SimConnect.dll will be copied from:' + NewLine;
    S := S + Space + SimConnectPath + NewLine;
    S := S + 'to:' + NewLine;
    S := S + Space + ExpandConstant('{app}\SimConnect.dll') + NewLine;
    S := S + NewLine;
  end else begin
    S := S + 'Existing SimConnect.dll found!' + NewLine;
  end;

  S := S + 'If you enjoy FSKneeboard FREE please consider upgrading to PRO at' + NewLine;
  S := S + 'https://fskneeboard.com/buy-now to unlock all features and support the development.' + NewLine;
  S := S + NewLine;
  S := S + 'Join us on Discord at https://discord.fskneeboard.com';
  Result := S;
end;

#include "fskneeboard-common.iss"