; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#ifndef ApplicationVersion
#define ApplicationVersion "0.0.1"
#endif

#define MyAppName "FSKneeboard FREE"
#define MyAppPublisher "Christian-Alexander Hoffmann"
#define MyAppURL "https://www.fskneeboard.com/"
#define MyAppExeName "fskneeboard.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{BFD7EA7D-F475-4DEE-AE14-52129352949E}
AppName={#MyAppName}
AppVersion={#ApplicationVersion}
AppVerName={#MyAppName} v{#ApplicationVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=..\LICENSE.txt
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=..\dist
OutputBaseFilename=Install-FSKneeboard-FREE-v{#ApplicationVersion}
SetupIconFile=..\fskneeboard-server\vfrmap\winres\fskneeboard.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\dist\free\fskneeboard-server\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\free\CHANGELOG.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\free\README.pdf"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "..\dist\free\fskneeboard-server\fskneeboard-autostart-steam.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-server\fskneeboard-autostart-windows-store.bat"; DestDir: "{app}"; Flags: ignoreversion

Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\layout.json"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\manifest.json"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\InGamePanels\christian1984-ingamepanel-fskneeboard.spb"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\InGamePanels"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\icons\toolbar\ICON_TOOLBAR_CHRISTIAN1984_INGAMEPANEL_VFRMAPFORVR.svg"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\icons\toolbar"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\Textures\Menu\toolbar\ICON_TOOLBAR_CHRISTIAN1984_INGAMEPANEL_VFRMAPFORVR.svg"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\Textures\Menu\toolbar"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.css"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.html"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "..\dist\free\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.js"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\FSKneeboard\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\{#MyAppName} + MSFS (Windows Store)"; Filename: "{app}\fskneeboard-autostart-windows-store.bat"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\{#MyAppName} + MSFS (Steam)"; Filename: "{app}\fskneeboard-autostart-steam.bat"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\Docs - Readme"; Filename: "{app}\README.pdf"
Name: "{autoprograms}\FSKneeboard\Docs - Changelog"; Filename: "{app}\CHANGELOG.md"
Name: "{autoprograms}\FSKneeboard\Web - Join us on Discord"; Filename: "https://discord.fskneeboard.com"
Name: "{autoprograms}\FSKneeboard\Web - Upgrade to PRO"; Filename: "https://fskneeboard.com/buy-now"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "https://discord.fskneeboard.com"; Flags: nowait shellexec runasoriginaluser postinstall; Description: "Join us on Discord!"
Filename: "https://fskneeboard.com/buy-now"; Flags: nowait shellexec runasoriginaluser postinstall; Description: "Upgrade to PRO!"

[UninstallDelete]
;This works if it is installed in custom location
Type: files; Name: "{app}\latestcheck.json"; 
Type: files; Name: "{app}\SimConnect.dll"; 

[Code]
var
  CommunityFolderDirWizardPage: TInputDirWizardPage;
  CommunityFolderDir: String;

function GetCommunityFolderDir(Value: string): string;
begin
    Result := CommunityFolderDir;
end;

procedure InitializeWizard;
var
  AfterID: Integer;
  communityFolderSuccess: Boolean;
  communityFolder: String;
  winstoreCommunityFolder: String;
  steamCommunityFolder: String;
  communityFolderDirWizardDescription: String;

begin
  AfterID := wpSelectDir;
  communityFolderSuccess := False;

  winstoreCommunityFolder := ExpandConstant('{localappdata}\Packages\Microsoft.FlightSimulator_8wekyb3d8bbwe\LocalCache\Packages\Community');
  steamCommunityFolder := ExpandConstant('{localappdata}\Packages\Microsoft.FlightDashboard_8wekyb3d8bbwe\LocalCache\Packages\Community');
  
  if DirExists(winstoreCommunityFolder) or DirExists(steamCommunityFolder) then begin
    communityFolderSuccess := True;
    communityFolderDirWizardDescription := 'SUCCESS! Automatically detected your Flight Simulator Community Folder.';

    if DirExists(steamCommunityFolder) then begin
        communityFolder := steamCommunityFolder;
        Log('steamCommunityFolder found!');
    end else if DirExists(winstoreCommunityFolder) then begin
        communityFolder := winstoreCommunityFolder;
        Log('winstoreCommunityFolder found!');
    end
  end else begin
    communityFolderDirWizardDescription := '*** PLEASE DO NOT CLICK NEXT BEFORE READING THIS ***'#13#10#13#10
    + 'WARNING: Your Flight Simulator Community Folder could NOT be auto-detected! Please set AND VERIFY the path to your community folder manually:'#13#10#13#10
    + '- WINDOWS STORE USERS: If you have purchased MSFS through the Windows Store, you will typically find it under C:\Users\[username]\AppData\Local\Packages\ '#13#10 + 'Microsoft.FlightSimulator_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10#13#10
    + '- STEAM USERS: If you have purchased MSFS through Steam, the default path for your Community Folder would typically be C:\Users\[username]\AppData\Local\Packages\ '#13#10 + 'Microsoft.FlightDashboard_8wekyb3d8bbwe\LocalCache\Packages\Community';

    communityFolder := 'C:\'
  end;

  CommunityFolderDirWizardPage := CreateInputDirPage(
        AfterID,
        'Select Community Folder Location',
        'Please tell us where your Flight Simulator Community Folder is located!',
        communityFolderDirWizardDescription,
        False, '');
  CommunityFolderDirWizardPage.Add('IMPORTANT: If the FSKneeboard-Ingame-Panel does NOT appear inside the game, then double-check that you have this directory right!' + #13#10#13#10 + 'Microsoft Flight Simulator Community Folder:');
  CommunityFolderDirWizardPage.Values[0] := communityFolder;

  if communityFolderSuccess then begin
    CommunityFolderDirWizardPage.SubCaptionLabel.Font.Color := clGreen;
  end else begin
    CommunityFolderDirWizardPage.SubCaptionLabel.Font.Color := clRed;
  end;

  CommunityFolderDirWizardPage.SubCaptionLabel.Font.Style := [fsBold];

  CommunityFolderDirWizardPage.PromptLabels[0].Font.Color := $0088FF;
  CommunityFolderDirWizardPage.PromptLabels[0].Font.Style := [fsBold];
  AfterID := CommunityFolderDirWizardPage.ID;
end;

function NextButtonClick(CurrPageID: Integer): Boolean;
begin
  if CurrPageID = CommunityFolderDirWizardPage.ID then begin
    CommunityFolderDir := CommunityFolderDirWizardPage.Values[0];
    Log('CommunityFolderDir is: ' + CommunityFolderDir);
  end;
  Result := True;
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo,
  MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
var
  S: String;
begin
  S := 'FSKneeboard FREE is Ready for Installation!' + NewLine;
  S := S + NewLine;
  S := S + 'FSKneeboard Server Component will be installed to:' + NewLine;
  S := S + Space + ExpandConstant('{app}') + NewLine;
  S := S + NewLine;
  S := S + 'FSKneeboard Ingame Panel will be installed to:' + NewLine;
  S := S + Space + CommunityFolderDir + '\christian1984-ingamepanel-fskneeboard' + NewLine;
  S := S + NewLine;
  S := S + 'If you enjoy FSKneeboard FREE please consider upgrading to PRO at https://fskneeboard.com/buy-now' + NewLine;
  S := S + NewLine;
  S := S + 'Join us on Discord at https://discord.fskneeboard.com';
  Result := S;
end;