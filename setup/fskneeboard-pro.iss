; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#ifndef ApplicationVersion
#define ApplicationVersion "0.0.1"
#endif

#define MyAppName "FSKneeboard PRO"
#define MyAppPublisher "Christian-Alexander Hoffmann"
#define MyAppURL "https://www.fskneeboard.com/"
#define MyAppExeName "fskneeboard.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{85F960AA-B1FF-4C01-BEF0-74D87689AE8E}
AppName={#MyAppName}
AppVersion={#ApplicationVersion}
AppVerName={#MyAppName} v{#ApplicationVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=..\LICENSE.txt
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=..\dist
OutputBaseFilename=Install-FSKneeboard-PRO-v{#ApplicationVersion}
SetupIconFile=..\fskneeboard-server\vfrmap\winres\fskneeboard.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\dist\pro\fskneeboard-server\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\pro\CHANGELOG.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\pro\README.pdf"; DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "..\dist\pro\fskneeboard-server\copy-your-license-file-here.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\repair-fskneeboard.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\charts\copy-your-charts-here.txt"; DestDir: "{app}\charts"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\charts\traffic-pattern.png"; DestDir: "{app}\charts"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\charts\traffic-pattern.png"; DestDir: "{app}\charts"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\charts\approach\MDW.png"; DestDir: "{app}\charts\approach"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\charts\weather\weather_forecast_chart.png"; DestDir: "{app}\charts\weather"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-server\autosave\autosaves-will-go-here.txt"; DestDir: "{app}\autosave"; Flags: ignoreversion

Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\layout.json"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\manifest.json"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\InGamePanels\christian1984-ingamepanel-fskneeboard.spb"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\InGamePanels"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\icons\toolbar\ICON_TOOLBAR_CHRISTIAN1984_INGAMEPANEL_VFRMAPFORVR.svg"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\icons\toolbar"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\Textures\Menu\toolbar\ICON_TOOLBAR_CHRISTIAN1984_INGAMEPANEL_VFRMAPFORVR.svg"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\Textures\Menu\toolbar"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.css"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.html"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion
Source: "..\dist\pro\fskneeboard-panel\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel\FSKneeboardPanel.js"; DestDir: "{code:GetCommunityFolderDir}\christian1984-ingamepanel-fskneeboard\html_ui\InGamePanels\FSKneeboardPanel"; Flags: ignoreversion

Source: "{code:GetLicenseFile}"; DestDir: "{app}"; DestName: "fskneeboard.lic"; Flags: external uninsneveruninstall; Check: GetShouldInstallLicenseFile
Source: "{code:GetSimConnectPath}"; DestDir: "{app}"; DestName: "SimConnect.dll"; Flags: external uninsneveruninstall; Check: GetShouldInstallSimConnect
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\FSKneeboard\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\Repair {#MyAppName}"; Filename: "{app}\repair-fskneeboard.bat"; IconFilename: "{app}\{#MyAppExeName}"
Name: "{autoprograms}\FSKneeboard\Docs - Readme"; Filename: "{app}\README.pdf"
Name: "{autoprograms}\FSKneeboard\Docs - Changelog"; Filename: "{app}\CHANGELOG.md"
Name: "{autoprograms}\FSKneeboard\Charts-Folder"; Filename: "{app}\charts"
Name: "{autoprograms}\FSKneeboard\Autosave-Folder"; Filename: "{app}\autosave"
Name: "{autoprograms}\FSKneeboard\Web - Join us on Discord"; Filename: "https://discord.fskneeboard.com"
; Name: "{autoprograms}\FSKneeboard\Web - Upgrade to PRO"; Filename: "https://fskneeboard.com/buy-now"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "https://discord.fskneeboard.com"; Flags: nowait shellexec runasoriginaluser postinstall; Description: "Join us on Discord!"

[UninstallDelete]
;This works if it is installed in custom location
Type: files; Name: "{app}\latestcheck.json"; 
Type: filesandordirs; Name: "{app}\logs"; 

[Code]
var
  CommunityFolderDirWizardPage: TInputDirWizardPage;
  CommunityFolderDir: String;
  CommunityFoldersListPage: TInputOptionWizardPage;
  DetectedCommunityFolders: TArrayOfString;
  CommunityFolderVersions: TArrayOfString;

  SimConnectWizardPage: TOutputMsgWizardPage;
  SimConnectFileEdit: TEdit;
  SimConnectFileButton: TButton;
  SimConnectPath: String;
  ShouldInstallSimConnect: Boolean;
  
  LicenseFileWizardPage: TInputFileWizardPage;
  LicenseFile: String;
  ShouldInstallLicenseFile: Boolean;

// Forward declarations for common functions/procedures
function ParseUserCfgOpt(FilePath: String): String; forward;
procedure DiscoverCommunityFolders(); forward;
function GetCommunityFolderDir(Value: string): string; forward;
function StrSplit(Text: String; Separator: String): TArrayOfString; forward;
function DirCopy(SourcePath, DestPath: String; Overwrite: Boolean): Boolean; forward;
procedure CreateSimConnectWizardPage(AfterID: Integer); forward;

function GetLicenseFile(Value: string): string;
begin
    Result := LicenseFile;
end;

function GetShouldInstallLicenseFile(): Boolean;
begin
    Result := ShouldInstallLicenseFile;
end;

function GetShouldInstallSimConnect: Boolean;
begin
  Result := ShouldInstallSimConnect;
end;

procedure InitializeWizard;
var
  AfterID: Integer;
  communityFolderSuccess: Boolean;
  communityFolder: String;
  winstoreCommunityFolder: String;
  steamCommunityFolder: String;
  communityFolderDirWizardDescription: String;
  i: Integer;
  FolderCount: Integer;

begin
  AfterID := wpSelectDir;
  communityFolderSuccess := False;

  // First try the new UserCfg.opt-based discovery
  DiscoverCommunityFolders();
  FolderCount := GetArrayLength(DetectedCommunityFolders);

  if FolderCount > 1 then
  begin
    // Multiple community folders found - create selection page
    CommunityFoldersListPage := CreateInputOptionPage(
      AfterID,
      'Select Community Folder',
      'Multiple Microsoft Flight Simulator installations detected!',
      'Please select the Community Folder where you want to install the FSKneeboard panel:',
      True, False);

    for i := 0 to FolderCount - 1 do
    begin
      CommunityFoldersListPage.Add(CommunityFolderVersions[i] + ': ' + DetectedCommunityFolders[i]);
    end;
    CommunityFoldersListPage.Values[0] := True; // Select first one by default

    AfterID := CommunityFoldersListPage.ID;
    communityFolderSuccess := True;
  end
  else if FolderCount = 1 then
  begin
    // Single community folder found - use it directly
    communityFolder := DetectedCommunityFolders[0];
    communityFolderSuccess := True;
    communityFolderDirWizardDescription := 'SUCCESS! Automatically detected your ' + CommunityFolderVersions[0] + ' Community Folder.';
    Log('Auto-detected community folder via UserCfg.opt: ' + communityFolder);
  end
  else
  begin
    // No folders found via UserCfg.opt, fall back to hardcoded detection
    winstoreCommunityFolder := ExpandConstant('{localappdata}\Packages\Microsoft.FlightSimulator_8wekyb3d8bbwe\LocalCache\Packages\Community');
    steamCommunityFolder := ExpandConstant('{localappdata}\Packages\Microsoft.FlightDashboard_8wekyb3d8bbwe\LocalCache\Packages\Community');
    
    if DirExists(winstoreCommunityFolder) or DirExists(steamCommunityFolder) then begin
      communityFolderSuccess := True;
      communityFolderDirWizardDescription := 'SUCCESS! Automatically detected your Flight Simulator Community Folder (fallback method).';

      if DirExists(steamCommunityFolder) then begin
          communityFolder := steamCommunityFolder;
          Log('steamCommunityFolder found via fallback!');
      end else if DirExists(winstoreCommunityFolder) then begin
          communityFolder := winstoreCommunityFolder;
          Log('winstoreCommunityFolder found via fallback!');
      end
    end else begin
      communityFolderDirWizardDescription := '*** PLEASE DO NOT CLICK NEXT BEFORE READING THIS ***'#13#10#13#10
      + 'WARNING: Your Flight Simulator Community Folder could NOT be auto-detected! Please set AND VERIFY the path to your community folder manually:'#13#10#13#10
      + '- WINDOWS STORE USERS (MSFS 2020): C:\Users\[username]\AppData\Local\Packages\Microsoft.FlightSimulator_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10
      + '- WINDOWS STORE USERS (MSFS 2024): C:\Users\[username]\AppData\Local\Packages\Microsoft.Limitless_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10#13#10
      + '- STEAM USERS (MSFS 2020): C:\Users\[username]\AppData\Local\Packages\Microsoft.FlightDashboard_8wekyb3d8bbwe\LocalCache\Packages\Community'#13#10
      + '- STEAM USERS (MSFS 2024): Varies by installation - check your UserCfg.opt file';

      communityFolder := 'C:\'
    end;
  end;

  // Only create the manual input page if we don't have the multi-selection page
  if FolderCount <= 1 then
  begin
    CommunityFolderDirWizardPage := CreateInputDirPage(
          AfterID,
          'Select Community Folder Location',
          'Please tell us where your Flight Simulator Community Folder is located!',
          communityFolderDirWizardDescription,
          False, '');
    CommunityFolderDirWizardPage.Add('IMPORTANT: If the FSKneeboard-Ingame-Panel does NOT appear inside the game, then double-check that you have this directory right!' + #13#10#13#10 + 'Microsoft Flight Simulator Community Folder:');
    CommunityFolderDirWizardPage.Values[0] := communityFolder;

    if communityFolderSuccess then begin
      CommunityFolderDirWizardPage.SubCaptionLabel.Font.Color := clGreen;
    end else begin
      CommunityFolderDirWizardPage.SubCaptionLabel.Font.Color := clRed;
    end;

    CommunityFolderDirWizardPage.SubCaptionLabel.Font.Style := [fsBold];

    CommunityFolderDirWizardPage.PromptLabels[0].Font.Color := $0088FF;
    CommunityFolderDirWizardPage.PromptLabels[0].Font.Style := [fsBold];

    AfterID := CommunityFolderDirWizardPage.ID;
  end;

  LicenseFileWizardPage := CreateInputFilePage(
      AfterID,
      'Select License File',
      'Please tell us where we can find your license file...'#13#10
      + '(Typically that would be something like C:\Users\[username]\Downloads)', '');
  LicenseFileWizardPage.Add('&License File:', 'FSKneeboard License Files|*.lic', '.lic');
  LicenseFileWizardPage.Values[0] := ExpandConstant('{%USERPROFILE}\Downloads\fskneeboard.lic');
  AfterID := LicenseFileWizardPage.ID;
end;

function NextButtonClick(CurrPageID: Integer): Boolean;
var
  licFilePath: String;
  i: Integer;
  SelectedFolders: String;
begin
  if CurrPageID = wpSelectDir then
  begin
    // Check if SimConnect.dll exists after the directory is selected
    ShouldInstallSimConnect := not FileExists(ExpandConstant('{app}\SimConnect.dll'));
    if ShouldInstallSimConnect then
    begin
      CreateSimConnectWizardPage(CurrPageID);
    end;
    
    licFilePath := ExpandConstant('{app}\fskneeboard.lic');
    if FileExists(licFilePath) then begin
      Log('Existsing license file found at ' + licFilePath + '!');
      ShouldInstallLicenseFile := False;
    end else begin
      Log('No License file found! (Looked at ' + licFilePath + ')');
      ShouldInstallLicenseFile := True;
    end;
  end
  else if ShouldInstallSimConnect and (CurrPageID = SimConnectWizardPage.ID) then
  begin
    SimConnectPath := SimConnectFileEdit.Text;
    if not FileExists(SimConnectPath) then
    begin
      MsgBox('The file ' + SimConnectPath + ' does not exist. Please select a valid SimConnect.dll file.', mbError, MB_OK);
      Result := False;
      Exit;
    end;
  end
  else if CurrPageID = wpWelcome then begin
    Log('Set ShouldInstallLicenseFile to True');
    ShouldInstallLicenseFile := True;
  end else if (GetArrayLength(DetectedCommunityFolders) > 1) and (CurrPageID = CommunityFoldersListPage.ID) then
  begin
    // Handle multiple folder selection
    SelectedFolders := '';
    for i := 0 to GetArrayLength(DetectedCommunityFolders) - 1 do
    begin
      if CommunityFoldersListPage.Values[i] then
      begin
        if SelectedFolders <> '' then
          SelectedFolders := SelectedFolders + ';';
        SelectedFolders := SelectedFolders + DetectedCommunityFolders[i];
      end;
    end;
    CommunityFolderDir := SelectedFolders;
    Log('Selected Community Folders: ' + CommunityFolderDir);
  end else if (GetArrayLength(DetectedCommunityFolders) <= 1) and (CurrPageID = CommunityFolderDirWizardPage.ID) then begin
    CommunityFolderDir := CommunityFolderDirWizardPage.Values[0];
    Log('CommunityFolderDir is: ' + CommunityFolderDir);
  end else if CurrPageID = LicenseFileWizardPage.ID then begin
    LicenseFile := LicenseFileWizardPage.Values[0];
    Log('LicenseFile is: ' + LicenseFile);

    if not FileExists(LicenseFile) then begin
        MsgBox('The License File ' + LicenseFile + ' does not exists. Please select your license file!'#13#10#13#10 + 'If you do not have a license file yet, please purchase a copy of FSKneeboard at https://fskneeboard.com/buy-now', mbError, MB_OK);
        Result := False
        Exit;
    end;
  end;
  Result := True;
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result := False;

  if ShouldInstallSimConnect and (PageID = SimConnectWizardPage.ID) then
    Result := False
  else if (not ShouldInstallSimConnect) and (SimConnectWizardPage <> nil) and (PageID = SimConnectWizardPage.ID) then
    Result := True
  else if PageID = LicenseFileWizardPage.ID then
    Result := not ShouldInstallLicenseFile;
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo,
  MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
var
  S: String;
  FolderPaths: TArrayOfString;
  i: Integer;
begin
  S := 'FSKneeboard PRO is Ready for Installation!' + NewLine;
  S := S + NewLine;
  S := S + 'FSKneeboard Server Component will be installed to:' + NewLine;
  S := S + Space + ExpandConstant('{app}') + NewLine;
  S := S + NewLine;

  S := S + 'FSKneeboard Ingame Panel will be installed to:' + NewLine;
  S := S + Space + CommunityFolderDir + '\christian1984-ingamepanel-fskneeboard' + NewLine;

  S := S + NewLine;

  if ShouldInstallSimConnect then
  begin
    S := S + 'SimConnect.dll will be copied from:' + NewLine;
    S := S + Space + SimConnectPath + NewLine;
    S := S + 'to:' + NewLine;
    S := S + Space + ExpandConstant('{app}\SimConnect.dll') + NewLine;
    S := S + NewLine;
  end else begin
    S := S + 'Existing SimConnect.dll found!' + NewLine;
  end;

  if ShouldInstallLicenseFile then begin
    S := S + 'FSKneeboard License File will be copied from:' + NewLine;
    S := S + Space + LicenseFile + NewLine;
    S := S + 'to:' + NewLine;
    S := S + Space + ExpandConstant('{app}\fskneeboard.lic') + NewLine;
  end else begin
    S := S + 'Existing FSKneeboard License File found!' + NewLine;
  end;

  S := S + NewLine;
  S := S + 'Join us on Discord at https://discord.fskneeboard.com';
  Result := S;
end;

#include "fskneeboard-common.iss"